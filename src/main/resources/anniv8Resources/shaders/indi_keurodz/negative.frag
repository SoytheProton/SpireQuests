#version 120

varying vec2 v_texCoord;
varying vec4 v_color;

uniform sampler2D u_texture;

float hue(float s, float t, float h) {
    float hs = mod(h, 1.0) * 6.0;
    if (hs < 1.0) return (t - s) * hs + s;
    if (hs < 3.0) return t;
    if (hs < 4.0) return (t - s) * (4.0 - hs) + s;
    return s;
}

vec4 RGB(vec4 c) {
    if (c.y < 0.0001)
        return vec4(vec3(c.z), c.a);

    float t = (c.z < 0.5) ? c.y * c.z + c.z : -c.y * c.z + (c.y + c.z);
    float s = 2.0 * c.z - t;
    return vec4(hue(s, t, c.x + 1.0 / 3.0), hue(s, t, c.x), hue(s, t, c.x - 1.0 / 3.0), c.w);
}

vec4 HSL(vec4 c) {
    float low = min(c.r, min(c.g, c.b));
    float high = max(c.r, max(c.g, c.b));
    float delta = high - low;
    float sum = high + low;

    vec4 hsl = vec4(0.0, 0.0, 0.5 * sum, c.a);
    if (delta == 0.0)
        return hsl;

    hsl.y = (hsl.z < 0.5) ? delta / sum : delta / (2.0 - sum);

    if (high == c.r)
        hsl.x = (c.g - c.b) / delta;
    else if (high == c.g)
        hsl.x = (c.b - c.r) / delta + 2.0;
    else
        hsl.x = (c.r - c.g) / delta + 4.0;

    hsl.x = mod(hsl.x / 6.0, 1.0);
    return hsl;
}

void main() {
    vec4 tex = texture2D(u_texture, v_texCoord);

    vec4 SAT = HSL(tex);

    SAT.b = (1.0 - SAT.b);
    SAT.r = -SAT.r + 0.2;

    tex = RGB(SAT) + 0.8 * vec4(79.0 / 255.0, 99.0 / 255.0, 103.0 / 255.0, 0.0);

    if (tex.a < 0.7) {
        tex.a = tex.a / 3.0;
    }

    tex.rgb *= tex.a;

    gl_FragColor = tex * v_color;
}
